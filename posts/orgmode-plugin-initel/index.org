#+BEGIN_COMMENT
.. title: Orgmode Plugin init.el
.. slug: orgmode-plugin-initel
.. date: 2023-06-25 20:39:18 UTC-07:00
.. tags: plugins,orgmode
.. category: Plugins
.. link: 
.. description: The emacs configuration for the orgmode plugin.
.. type: text

#+END_COMMENT

This is the emacs configuration (~init.el~) for the [[https://plugins.getnikola.com/v8/orgmode/][orgmode plugin]] to export posts and pages to HTML. I spent forever messing with the ~init.el~ file for my emacs instance, but it turns out that the plugin has its own configuration so you have to make changes here or they won't matter.

* Pygments
This is the list of languages that pygments will colorize when source-code blocks are exported. The org-babel documentation has a [[https://orgmode.org/worg/org-contrib/babel/languages/index.html][list of supported]] languages, but it doesn't list ~fish~, although that seems to work. It might be because I inserted fish-mode, or maybe it just has to be a [[https://pygments.org/languages/][supported pygments language]].

The first argument is what you put as the type in the source block and the second is the pygments short-name ~("org-source-name" . "pygments-name")~.

** Fish
Add coloring for the fish-shell. 

#+begin_src emacs-lisp :noweb-ref fish-pygments
("fish" . "fish")
#+end_src


#+begin_src fish
function color_test
  echo "colorize me"
end
#+end_src
** Gherkin

#+begin_src emacs-lisp :noweb-ref gherkin-pygments
("gherkin" . "gherkin")
#+end_src

#+begin_src gherkin
Scenario: Gherkin colorization

Feature: Pygments gherkin colorization

Given a gherkin feature file
When the org-mode file is exported to HTML
Then the feature fragment is colorized.
#+end_src
** HTML

#+begin_src emacs-lisp :noweb-ref html-pygments
("html" . "html")
#+end_src

#+begin_src html
<h1>Colorize HTML</h1>
#+end_src

** Jinja

#+begin_src emacs-lisp :noweb-ref jinja-pygments
("jinja" . "html+jinja")
#+end_src

#+begin_src jinja
{% macro pseudocode() %}
  <script src="https://cdn.jsdelivr.net/npm/pseudocode@latest/build/pseudocode.min.js"></script>
{% endmacro %}
#+end_src

** Mako

#+begin_src emacs-lisp :noweb-ref mako-pygments
("mako" . "html+mako")
#+end_src

#+begin_src mako
<%def name="pseudocode()">
<script src="https://cdn.jsdelivr.net/npm/pseudocode@latest/build/pseudocode.min.js"></script>
</%def>
#+end_src

* The Tangle
#+begin_src emacs-lisp :tangle ../plugins/orgmode/init.el
;; Init file to use with the orgmode plugin.

;; this is generated from the post with the slug: orgmode-plugin-initel

;; Load org-mode
;; Requires org-mode v8.x

(require 'package)
(setq package-load-list '((htmlize t)))
(package-initialize)

(require 'org)
(require 'ox-html)

;;; Custom configuration for the export.

;;; Add any custom configuration that you would like to 'conf.el'.
(setq nikola-use-pygments t
      org-export-with-toc nil
      org-export-with-section-numbers nil
      org-startup-folded 'showeverything)

;; Load additional configuration from conf.el
(let ((conf (expand-file-name "conf.el" (file-name-directory load-file-name))))
  (if (file-exists-p conf)
      (load-file conf)))

;;; Macros

;; Load Nikola macros
(setq nikola-macro-templates
      (with-current-buffer
          (find-file
           (expand-file-name "macros.org" (file-name-directory load-file-name)))
        (org-macro--collect-macros)))

;;; Code highlighting
(defun org-html-decode-plain-text (text)
  "Convert HTML character to plain TEXT. i.e. do the inversion of
     `org-html-encode-plain-text`. Possible conversions are set in
     `org-html-protect-char-alist'."
  (mapc
   (lambda (pair)
     (setq text (replace-regexp-in-string (cdr pair) (car pair) text t t)))
   (reverse org-html-protect-char-alist))
  text)

;; Use pygments highlighting for code
(defun pygmentize (lang code)
  "Use Pygments to highlight the given code and return the output"
  (with-temp-buffer
    (insert code)
    (let ((lang (or (cdr (assoc lang org-pygments-language-alist)) "text")))
      (shell-command-on-region (point-min) (point-max)
                               (format "pygmentize -f html -l %s" lang)
                               (buffer-name) t))
    (buffer-string)))

(defconst org-pygments-language-alist
  '(("asymptote" . "asymptote")
    ("awk" . "awk")
    ("c" . "c")
    ("console" . "console")
    ("c++" . "cpp")
    ("cpp" . "cpp")
    ("clojure" . "clojure")
    ("css" . "css")
    ("d" . "d")
    ("emacs-lisp" . "scheme")
    ("F90" . "fortran")
    <<fish-pygments>>
    <<gherkin-pygments>>
    ("gnuplot" . "gnuplot")
    ("groovy" . "groovy")
    ("haskell" . "haskell")
    <<html-pygments>>
    ("java" . "java")
    <<jinja-pygments>>
    ("js" . "js")
    ("julia" . "julia")
    ("latex" . "latex")
    ("lisp" . "lisp")
    <<mako-pygments>>
    ("makefile" . "makefile")
    ("matlab" . "matlab")
    ("mscgen" . "mscgen")
    ("ocaml" . "ocaml")
    ("octave" . "octave")
    ("perl" . "perl")
    ("picolisp" . "scheme")
    ("python" . "python")
    ("r" . "r")
    ("ruby" . "ruby")
    ("sass" . "sass")
    ("scala" . "scala")
    ("scheme" . "scheme")
    ("sh" . "sh")
    ("shell-session" . "shell-session")
    ("sql" . "sql")
    ("sqlite" . "sqlite3")
    ("tcl" . "tcl"))
  "Alist between org-babel languages and Pygments lexers.
lang is downcased before assoc, so use lowercase to describe language available.
See: http://orgmode.org/worg/org-contrib/babel/languages.html and
http://pygments.org/docs/lexers/ for adding new languages to the mapping.")

;; Override the html export function to use pygments
(defun org-html-src-block (src-block contents info)
  "Transcode a SRC-BLOCK element from Org to HTML.
CONTENTS holds the contents of the item.  INFO is a plist holding
contextual information."
  (if (org-export-read-attribute :attr_html src-block :textarea)
      (org-html--textarea-block src-block)
    (let ((lang (org-element-property :language src-block))
          (code (org-element-property :value src-block))
          (code-html (org-html-format-code src-block info)))
      (if nikola-use-pygments
          (progn
            (unless lang (setq lang ""))
            (pygmentize (downcase lang) (org-html-decode-plain-text code)))
        code-html))))

;; Export images with custom link type
(defun org-custom-link-img-url-export (path desc format)
  (cond
   ((eq format 'html)
    (format "<img src=\"%s\" alt=\"%s\"/>" path desc))))
(org-add-link-type "img-url" nil 'org-custom-link-img-url-export)

;; Export images with built-in file scheme
(defun org-file-link-img-url-export (path desc format)
  (cond
   ((eq format 'html)
    (format "<img src=\"/%s\" alt=\"%s\"/>" path desc))))
(org-add-link-type "file" nil 'org-file-link-img-url-export)

;; Support for magic links (link:// scheme)
(org-link-set-parameters
  "link"
  :export (lambda (path desc backend)
             (cond
               ((eq 'html backend)
                (format "<a href=\"link:%s\">%s</a>"
                        path (or desc path))))))

;; Export function used by Nikola.
(defun nikola-html-export (infile outfile)
  "Export the body only of the input file and write it to
specified location."
  (with-current-buffer (find-file infile)
    (org-macro-replace-all nikola-macro-templates)
    (org-html-export-as-html nil nil t t)
    (write-file outfile nil)))
#+end_src

* Links
- Chaganti P. Plugins for Nikola. 2020 [cited 2023 Jun 25]. orgmode. Available from: https://plugins.getnikola.com/v8/orgmode/

- Babel: Languages [Internet]. [cited 2023 Jun 25]. Available from: https://orgmode.org/worg/org-contrib/babel/languages/index.html

- Languages â€” Pygments [Internet]. [cited 2023 Jun 25]. Available from: https://pygments.org/languages/
